#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : COL_PARAMETRAGE_PDF
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x1a9521c402bb6eea
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  type_code : 31
  p_codes :
   -
     internal_properties : CAAAAAgAAADxw5Qf88SdltFqh4gsJ0ZfMWtuLAZ3h4tJMIumiFVJyVtWmFglQnXhkvnPFsxyC5yB/FgUhlo5/Y3U9+jFiwdrwgobbk4TG31XKdHfY6WpechCu5LXFk1s57+EP0SkFMeWglbMCzGUtLoczZY=
     code : |1-
      
      // Structure pour mémoriser les informations supplémentaires de chaque fichier
      STInformationsSupplémentaires est une structure
      	CheminComplet est une chaine <serialise = "path"> // Chemin complet du fichier
      	ModeAffichage est un entier <sérialise = "mode"> // Mode d'affichage (1 page, 2 pages, ...)
      	Zoom est un entier <Serialise = "zoom"> // Niveau de zoom
      fin
      
      // Tableau contenant les informations supplémentaires des fichiers
      gtabInformationsSupplémentaires est un tableau de STInformationsSupplémentaires
      
      // Charge les informations
      InformationsSupplémentairesCharge()
      
      // Indique si les paramètres d'affichage (mode, zoom, ...) sont restaurés à la réouverture d'un document
      gbRestaureParamètresAffichages est un booleen = ChargeParamètre("RestoreDisplay", faux)
     type : 720896
   -
     code : |1+
      
      // Sauvegarde les informations
      InformationsSupplémentairesSauve()
     type : 720898
  procedures :
   -
     name : InformationsSupplémentairesCharge
     internal_properties : CAAAAAgAAAB6y1WfA9T08S7CD3hvSavuqFFWlapXWXw1G5oU8EEwN3ajMXEXW/EkKXareTiwdmns2wWgWsQdHIt7aFLb2uXY/bv/KIzii75bFDrF1wpzeP9cN5pxRBMIKsSR0i8iwxIX9I6V9vvSizT2XkWh
     procedure_id : 1915475158303004086
     type_code : 15
     code : |1-
      // Résumé : Charge les informations supplémentaires mémorisées
      // Syntaxe :
      //InformationsSupplémentairesCharge ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      PROCEDURE InformationsSupplémentairesCharge()
      
      // Définit le chemin du fichier
      sFichier est une chaine = fRepDonnéesUtilisateur() + [fSep] + "pdf.json"
      
      // Vide le tableau 
      gtabInformationsSupplémentaires.SupprimeTout()
      
      // Vérifie l'existence du fichier
      SI fFichierExiste( sFichier ) = Faux ALORS retour
      
      // Charge les données
      sJSON est une chaine = fChargeTexte( sFichier )
      
      // Si le fichier est vide, ne rien faire
      SI sJSON = "" ALORS retour
      
      // Désérialise le contenu
      Désérialise(gtabInformationsSupplémentaires, sJSON, psdJSON)
      
      // Supprime les entrées les plus anciennes
      TANTQUE gtabInformationsSupplémentaires..Occurrence > 100
      	gtabInformationsSupplémentaires.Supprime( DernierElément )
      FIN
     type : 458752
   -
     name : InformationsSupplémentairesSauve
     internal_properties : CAAAAAgAAACaqzW/41Z+Ad6Cf9jf2Xv+eLFGhYpnGbyVS1qE8IFQVxYTcTFHewEUmZZ7OYjA5tm8+1VQOtQVJLNTYAojYm1wFcOHMHRaA6ZDjLJ9vxL7kJdEX+IZXCvwMtxpKlcK6yoP3PY9XlNQ61TWviVB
     procedure_id : 1915475841202896989
     type_code : 15
     code : |1+
      // Résumé : Sauve les informations supplémentaires
      // Syntaxe :
      //InformationsSupplémentairesSauve ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      PROCEDURE InformationsSupplémentairesSauve()
      
      sJSON est une chaine ansi
      
      // Définit le chemin du fichier
      sFichier est une chaine = fRepDonnéesUtilisateur() + [fSep] + "pdf.json"
      
      // Sérialise les données
      Sérialise(gtabInformationsSupplémentaires, sJSON, psdJSON)
      
      // Sauvegarde locale
      fSauveTexte( sFichier, sJSON)
     type : 458752
   -
     name : InformationsSupplémentairesAjoute
     procedure_id : 1915476244929851746
     type_code : 15
     code : |1+
      // Résumé : Ajoute des informations supplémentaires pour un fichier
      // Syntaxe :
      //InformationsSupplémentairesAjoute (<sChemin> est chaîne, <nModeAffichage> est entier, <nZoom> est entier)
      //
      // Paramètres :
      //	sChemin (chaîne ANSI) : Chemin complet du fichier
      //	nModeAffichage (entier) : Mode d'affichage
      //	nZoom (entier) : Zoom
      // Valeur de retour :
      // 	Aucune
      //
      PROCEDURE InformationsSupplémentairesAjoute(LOCAL sChemin est une chaine, LOCAL nModeAffichage est un entier, LOCAL nZoom est un entier)
      
      // Vérifie si ce document est déjà dans la liste
      nIndice est un entier = Cherche( gtabInformationsSupplémentaires, tcLinéaire + tccSansCasse, "CheminComplet", sChemin )
      
      SI nIndice > 0 ALORS
      		
      	// Mise à jour des informations
      	gtabInformationsSupplémentaires[ nIndice ].ModeAffichage = nModeAffichage
      	gtabInformationsSupplémentaires[ nIndice ].Zoom = nZoom
      	
      SINON
      	
      	// Ajout des informations
      	gtabInformationsSupplémentaires.Ajoute( [ sChemin, nModeAffichage, nZoom ] )		
      	
      FIN
      
      // Sauve le tableau
      InformationsSupplémentairesSauve()
     type : 458752
   -
     name : InformationsSupplémentairesRécupère
     procedure_id : 1915477116814509979
     type_code : 15
     code : |1+
      // Résumé : Récupère les informations supplémentaires pour un fichier
      // Syntaxe :
      //[ <Résultat> = ] InformationsSupplémentairesRécupère (<sChemin> est chaîne)
      //
      // Paramètres :
      //	sChemin (chaîne ANSI) : Chemin complet du fichier
      // Valeur de retour :
      // 	multi-valeur : Mode d'affichage, niveau de zoom
      
      PROCEDURE InformationsSupplémentairesRécupère(LOCAL sChemin est une chaine) : (entier, entier)
      
      // Vérifie si ce document est dans la liste
      nIndice est un entier = Cherche( gtabInformationsSupplémentaires, tcLinéaire + tccSansCasse, "CheminComplet", sChemin )
      SI nIndice <= 0 ALORS renvoyer (-1, -1)
      
      // Renvoie les informations mémorisées
      renvoyer ( gtabInformationsSupplémentaires[nIndice].ModeAffichage, gtabInformationsSupplémentaires[nIndice].zoom )
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
  original_name : COL_SansNom1
resources :
 string_res :
  identifier : 0x1fc4c490000ce599
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
